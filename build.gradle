plugins {
    id 'com.palantir.docker' version '0.25.0' apply false
    id 'org.jetbrains.kotlin.jvm' version "${kotlin_version}"
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.palantir.docker'

ext {
    sharedDir = file("${project.rootDir}/shared")
}

group = 'com.exactpro.th2'
version = release_version

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    maven {
        name 'MavenLocal'
        url sharedDir
    }

    mavenCentral()

    maven {
        url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
    }

    maven {
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
    }

    mavenLocal()

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

jar {
    manifest {
        attributes(
                'Created-By': "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})",
                'Specification-Title': '',
                'Specification-Vendor': 'Exactpro Systems LLC',
                'Implementation-Title': project.archivesBaseName,
                'Implementation-Vendor': 'Exactpro Systems LLC',
                'Implementation-Vendor-Id': 'com.exactpro',
                'Implementation-Version': project.version
        )
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

clean {
    delete sharedDir
}

dependencies {
    api platform('com.exactpro.th2:bom:3.0.0')

    implementation 'com.exactpro.th2:common:3.22.0'
    implementation 'com.exactpro.th2:grpc-conn:0.0.1'
    implementation 'com.exactpro.th2:conn-ws-client:0.2.2'
    implementation 'com.exactpro.th2:conn-http-client:0.1.0'

    implementation 'org.slf4j:slf4j-log4j12:1.7.32'
    implementation 'org.slf4j:slf4j-api:1.7.32'

    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8'
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect'
    implementation group: 'io.github.microutils', name: 'kotlin-logging', version: '1.7.9'
    implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: '2.11.2'
    implementation group: 'com.athaydes.rawhttp', name:'rawhttp-core', version: '2.4.1'

    testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit5'
}

application {
    mainClassName 'com.exactpro.th2.ws.client.Main'
}

applicationName = 'service'

distTar {
    archiveName "${applicationName}.tar"
}

dockerPrepare {
    dependsOn distTar
}

docker {
    copySpec.from(tarTree("$buildDir/distributions/${applicationName}.tar"))
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
        freeCompilerArgs += "-XXLanguage:+UnitConversion"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "11"
        freeCompilerArgs += "-XXLanguage:+UnitConversion"
    }
}
